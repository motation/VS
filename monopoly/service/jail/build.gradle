buildscript {
    ext {
        springBootVersion = '1.2.7.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.bmuschko:gradle-docker-plugin:2.6'
    }
}

/**
 * Load several plugins, consult the gradle documentation for more info
 */
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-remote-api'

// imports for DockerTasks
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.container.DockerRemoveContainer



/**
 * Define the name and version of the resulting jar file
 */
jar {
    baseName = 'jail-service'
    version = '0.0.1-SNAPSHOT'
}

/**
 * Define source and target java version compatibility
 */
sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'vspai'
mainClassName = 'de.hawhamburg.monopoly.service.jail.JailServiceApplication'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // Can't find spring-cloud-build in maven central...
        url 'http://maven.springframework.org/release'
    }
}

//exclude tomcat
configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

/**
 * Configure project internal as well as external dependencies
 */
dependencies {
    // TODO: Added ver no on the three service starter deps to make the maven plugin to work

    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
    compile('javax.ws.rs:javax.ws.rs-api:2.0-m01')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

docker {
    /* TODO !IMPORTANT!
     * set this to you desired docker daemon. In case of local docker via docker-toolbox
     * find out the ip of your docker-machine via 'docker-machine url default'
     * TODO: Keep the https:// !
     */
    if (System.properties['os.name'].toString().toLowerCase().contains('windows')){
        url = 'https://192.168.99.100:2376'
        // set path to the certificate directory on your machine
        certPath = new File(System.properties['user.home'],'/.docker/machine/certs')
    }
    if(System.properties['os.name'].toString().toLowerCase().contains('linux')){
        url = 'https://127.0.0.1:2376'
        // set path to the certificate directory on your machine
        certPath = new File(System.properties['user.home'],'/.docker/machine/machines/default')
    }
}

/**
 * This task copies the tar file created by the distTar task from
 * the distributions folder to the docker folder. The distTar task
 * in return is called from the build task, which is a dependency
 * of this task.
 */
task copyDist(type: Copy, dependsOn: [build, wrapper]) {
    from('build/distributions/' + jar.baseName + '.tar')
    into('build/docker/')
}


/**
 * This task creates the Dockerfile needed to create the Docker image.
 * It inherits the java:8 docker image, sets some Spring Docker Profile variable,
 * exposes port 8080 to the outer world and adds our tar file as content at the
 * root path of the image.
 */
task createDockerfile(type: Dockerfile, dependsOn: [copyDist]) {
    destFile = project.file('build/docker/Dockerfile')
    from 'java:8'
    maintainer 'Sven-Ole Fedders "sven-ole.fedders@haw-hamburg.de"'
    environmentVariable('JAVA_OPTS', '-Dspring.profiles.active=docker')
    exposePort(4567)
    entryPoint("/jail-service/bin/jail-service")
    addFile({
        jar.baseName + '.tar'
    }, {
        '/'
    })
}

/**
 * This task builds the actual image of this service by using the previously
 * created Dockerfile from the ./build/docker folder and applying a talking tag
 * to the image
 */
task buildImage(type: DockerBuildImage, dependsOn: [createDockerfile]) {
    inputDir = createDockerfile.destFile.parentFile
    tag = 'jail-service'
}